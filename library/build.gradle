apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.alexjlockwood'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    implementation "com.android.support:support-compat:$supportLibVersion"
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

android.libraryVariants.all { variant ->
    def name = variant.name
    task "javadoc$name"(type: Javadoc) {
        description = "Generates javadocs for build variant: $name"
        failOnError = false
        destinationDir = new File(destinationDir, variant.baseName)
        source = files(variant.javaCompiler.source)
        classpath = files(variant.javaCompiler.classpath.files) + files(android.bootClasspath)
        options.windowTitle("Kyrie Reference")
        options.docTitle("Kyrie Reference")
        options.header("Kyrie Reference")
        exclude '**/R.java', '**/BuildConfig.java'
    }
}
